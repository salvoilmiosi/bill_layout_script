### Bill Layout Script
### Language it_IT

### Box Convalida Layout
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 1 0.11730205 0.16585466 0.2580645 0.04457344
### Goto Label convalida
### Script
$ifnot($contains(@,"www.energrid.it")) {
  $error("EnerGrid: Stringa di Convalida Errata")
}
### End Script
### End Box

### Box Nome Fornitore
### Type BOX_NOREAD
### Mode MODE_DEFAULT
### Rect 1 0.05283649 0.015727598 0.12235818 0.09436559
### Goto Label nome_fornitore
### Script
fornitore="EnerGrid"
### End Script
### End Box

### Box Dati Cliente
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 1 0.6539589 0.023841608 0.32111436 0.114025086
### Script
numero_cliente=$search(@,/CODICE CLIENTE: (.+)/)
ragione_sociale=$singleline($search(@,/Intestatario contratto: ([^]+)\nCod\. Fiscale/))
### End Script
### End Box

### Box Numero Fattura
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 1 0.13343109 0.30683112 0.6979472 0.04353685
### Script
numero_fattura=$search(@,/Fattura Energia n\u00b0 ([0-9]+)/)
data_fattura=$date(@,"%d/%m/%Y",/del (\D)/)
### End Script
### End Box

### Box Dati Fattura
### Type BOX_RECTANGLE
### Mode MODE_LAYOUT
### Rect 1 0.035190612 0.34902042 0.23020528 0.45257592
### Script
data_scadenza=$date(@,"%d/%m/%Y",/Scadenza fattura: (\D)/)

mese_fattura=$month(@,"%d/%m/%Y",/\D - (\D)/)
$ifnot($isset(mese_fattura)) {
  $error("EnerGrid: Mese Fattura Mancante")
}
_mese=$date_format(mese_fattura,"%m/%Y")
### End Script
### End Box

### Box Riepologo Importi
### Type BOX_RECTANGLE
### Mode MODE_LAYOUT
### Rect 1 0.2672123 0.350057 0.69011915 0.45153934
### Script
%totale_fattura=$search(@,/TOTALE DELLA FATTURA (\N)/)
codice_pod=$search(@,/POD: (.+)/)
indirizzo_fornitura=$search(@,/Indirizzo di fornitura: (.+)/)
### End Script
### End Box

### Box Dati Fornitura
### Type BOX_RECTANGLE
### Mode MODE_RAW
### Rect 2 0.045739044 0.10376283 0.91132927 0.21464685
### Spacers
p+::off_pod
### End Spacers
### Script
indirizzo_fornitura=$singleline($search(@,/Indirizzo di fornitura: ([^]+?\d{5}.+)\n/))
codice_pod=$search(@,/POD(?: \(punto di prelievo\)|:) (.+)/)
%potenza[0:3]=$search(@,/Potenza contrattuale: (\N) kW/)
### End Script
### End Box

### Box Storico Consumi / Imposte
### Type BOX_PAGE
### Mode MODE_LAYOUT
### Rect 2 0.043880455 0.5927153 0.9120019 0.23200065
### Spacers
p+::off_pod
### End Spacers
### Script
$between("RIEPILOGO CONSUMI PRIMO ADDEBITO","TOTALE (Salvo conguagli, errori e omissioni)") {
  $tokens($search(@,$format(/\d\d\/$0 Consumi Fatturati (\N \N \N \N \N \N \N)/,_mese))) {
    %energia_attiva_rilevata[0] %energia_attiva_rilevata[1] %energia_attiva_rilevata[2] $skip()
    %energia_reattiva[0] %energia_reattiva[1] %energia_reattiva[2]
  }
}
$between("DETTAGLIO IMPOSTE","TOTALE (Salvo conguagli, errori e omissioni)") {
  %accise=$search(@,$format(/Imposta erariale.+ al \d\d\/$0 Euro\/kWh \N \N (\N)/,_mese))
  iva=$percent($search(@,/(\d+) %/))
}
### End Script
### End Box

### Box Cerca Dettaglio
### Type BOX_RECTANGLE
### Mode MODE_LAYOUT
### Rect 3 0.03548387 0.02622577 0.92741936 0.09920182
### Goto Label cerca_dettaglio
### Spacers
p+_off_dettaglio
### End Spacers
### Script
$if($ate()) $error("EnerGrid: Dettaglio Mancante")
_pod_dettaglio=$search(@,/POD: (.+)/)
$if(_pod_dettaglio!=codice_pod) {
  _off_dettaglio+=1
  $goto(cerca_dettaglio)
}
### End Script
### End Box

### Box Dettaglio Importi
### Type BOX_PAGE
### Mode MODE_LAYOUT
### Rect 3 0.34193546 0.12884834 0.0483871 0.031927034
### Spacers
p+_off_dettaglio
### End Spacers
### Script
$between("DETTAGLIO IMPORTI PRIMO ADDEBITO","TOTALE (Salvo conguagli, errori e omissioni)") {
  $between("Spese per la materia energia","Spese per trasporto") {
    %spesa_materia_energia=$search(@,/(\N)/)
    $tokens($search(@,$format(/Componente di Generazione fascia F1 .+ al \d\d\/$0 Euro\/kWh (\N \N)/,_mese))) {
      %prezzo_energia[0] %energia_attiva[0]
    }
    $tokens($search(@,$format(/Componente di Generazione fascia F2 .+ al \d\d\/$0 Euro\/kWh (\N \N)/,_mese))) {
      %prezzo_energia[1] %energia_attiva[1]
    }
    $tokens($search(@,$format(/Componente di Generazione fascia F3 .+ al \d\d\/$0 Euro\/kWh (\N \N)/,_mese))) {
      %prezzo_energia[2] %energia_attiva[2]
    }
  }
  $between("Spese per trasporto","Spese per oneri") {
    %trasporto_gestione=$search(@,/(\N)/)
    $lines($searchall(@,$format(/Energia Reattiva tra il \d\d e il 75% fascia F[1-3] .+ al \d\d\/$0 Euro\/kvarh \N \N (\N)/,_mese))) %penale_reattiva_inf75+
    $lines($searchall(@,$format(/Energia Reattiva oltre il 75% fascia F[1-3] .+ al \d\d\/$0 Euro\/kvarh \N \N (\N)/,_mese))) %penale_reattiva_sup75+
  }
  $newview {
    $setbegin($indexof(@,"Spese per oneri"))
    %oneri=$search(@,/(\N)/)
  }
}
### End Script
### End Box

### Box Prossima Pagina
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 3 0.017741935 0.034207527 0.31774193 0.15507412
### Goto Label check_pagina
### Spacers
p+::off_pod
### End Spacers
### Script
$ifnot($ate() || $contains(@,"INFORMAZIONI AI CLIENTI")) {
  ::off_pod+=1
  $if($contains(@,"DATI FORNITURA")) {
    $nexttable()
    $goto(nome_fornitore)
  } $else {
    $goto(check_pagina)
  }
}
### End Script
### End Box
