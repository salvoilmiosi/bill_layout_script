### Bill Layout Script
### Language it_IT

### Box
### Name Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.03387097
### Y 0.096921325
### W 0.36451614
### H 0.044469774
### Script
$ifnot($contains(@,"Servizio di Maggior Tutela")) {
  $error("SEN: Stringa di Convalida Errata")
}
### End Script
### End Box

### Box
### Name Calcolo Offset Dettaglio
### Type RECTANGLE
### Mode DEFAULT
### Page 3
### X 0.054607615
### Y 0.21641847
### W 0.33431017
### H 0.028183728
### Goto Label calcolo_offset
### Spacers
p+::offset_dettaglio
### End Spacers
### Script
$if ($ate()) {
  $error("SEN: Dettaglio Mancante")
} $elif (@ != "DETTAGLIO IMPORTI BOLLETTA") {
  ::offset_dettaglio+=1
  $goto(calcolo_offset)
}
### End Script
### End Box

### Box
### Name Contenuto Dettaglio Costi
### Type RECTANGLE
### Mode LAYOUT
### Page 3
### X 0.048197635
### Y 0.21550742
### W 0.6294624
### H 0.76047003
### Goto Label dettaglio_costi
### Spacers
p+::offset_dettaglio y-::spacer_dettaglio h+::spacer_dettaglio
### End Spacers
### Script
::_dettaglio+=@
::_dettaglio+="\n"
### End Script
### End Box

### Box
### Name Spacer prima pagina dettaglio
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.048348106
### Y 0.005701254
### W 0.032232072
### H 0.20980616
### Spacers
p+::offset_dettaglio
### End Spacers
### Script
::spacer_dettaglio=$boxheight()
$ifnot($ate()) {
  ::offset_dettaglio+=1
  $goto(dettaglio_costi)
}
### End Script
### End Box

### Box
### Name Nome Fornitore
### Type NOREAD
### Mode DEFAULT
### Page 1
### X 0.06612903
### Y 0.035347775
### W 0.22096774
### H 0.061573543
### Goto Label nome_fornitore
### Script
fornitore=$coalesce(::fornitore,"SEN")
### End Script
### End Box

### Box
### Name Dati Fornitura
### Type RECTANGLE
### Mode RAW
### Page 1
### X 0.052688174
### Y 0.1687363
### W 0.45327735
### H 0.17021497
### Script
indirizzo_fornitura=$search($singleline(@),/Forniamo energia in (.+) Denominazione/)
%potenza[0:3]=$coalesce(
  $search(@,/Potenza contrattualmente impegnata (%N)/),
  $search(@,/Livello massimo di potenza (%N)/))
### End Script
### End Box

### Box
### Name N. Cliente / POD
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.05523641
### Y 0.34129426
### W 0.21984091
### H 0.103872165
### Script
numero_cliente=$search(@,/N\u00b0 CLIENTE (.+)/)
codice_pod=$search(@,/CODICE POD (.+)/)
### End Script
### End Box

### Box
### Name Dati Bolletta
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.3004861
### Y 0.36238107
### W 0.19774635
### H 0.04373565
### Script
numero_fattura=$search(@,/N\. Fattura (.+)/)
data_fattura=$date(@,"%d.%m.%Y",/Del (%D)/)
### End Script
### End Box

### Box
### Name Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.29827663
### Y 0.4100217
### W 0.20267215
### H 0.040712774
### Script
mese_fattura=$month(@,"%b.%Y")
mese_fine=$month(@,"%b.%Y",/%D - (%D)/)
$if($isset(mese_fine)) {
  mese_fattura=$month_add(mese_fattura,::calc_mese)
  ::calc_mese+=1
}
_mese=$date_format(mese_fattura,"%m/%y")
### End Script
### End Box

### Box
### Name Dettaglio Fiscale
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.05043084
### Y 0.6717093
### W 0.6890491
### H 0.29750395
### Script
%cts=$search(@,/CORRISPETTIVO TARIFFARIO SPECIFICO.+ (%N)/)
iva=$search(@,/IVA.* (\d+%)/)
### End Script
### End Box

### Box
### Name Totale Fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.51906633
### Y 0.36861986
### W 0.21763146
### H 0.035925712
### Script
%totale_fattura=$search(@,/(%N)/)
$ifnot($isset(totale_fattura)) {
  $error("SEN: Storno")
}
data_scadenza=$date(@,"%d.%m.%Y",/Entro il (%D)/)
### End Script
### End Box

### Box
### Name Ragione sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.51606274
### Y 0.19721653
### W 0.39770216
### H 0.031239748
### Script
ragione_sociale
### End Script
### End Box

### Box
### Name Consumi rilevati
### Type PAGE
### Mode RAW
### Page 2
### X 0.05040323
### Y 0.36969072
### W 0.66280246
### H 0.048995156
### Script
$between("Consumi rilevati", "INFORMAZIONI PER I CLIENTI") {
  $tokens($search(@,$format(/al \d\d\.$0 (.+)\n/,$date_format(mese_fattura,"%m.%Y")))) {
    %energia_attiva_rilevata[0] %energia_attiva_rilevata[1] %energia_attiva_rilevata[2]
    %energia_reattiva[0] %energia_reattiva[1] %energia_reattiva[2]
    $skip() $skip() $skip()
    %potenza[0] %potenza[1] %potenza[2]
  }
}
### End Script
### End Box

### Box
### Name Parse Dettaglio Costi
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.0903738
### Y 0.0063137496
### W 0.053058922
### H 0.03473528
### Script
_mese_full=$date_format(mese_fattura,"%B %Y")
_mese_abbr=$date_format(mese_fattura,"%m/%Y")
$with(::_dettaglio) {
  $between("SPESA PER LA MATERIA ENERGIA","TOTALE SPESA PER LA MATERIA ENERGIA") {
    $lines($searchall(@,$format(/$0[^]+?mesi %N (%N)/,_mese_full))) %spesa_materia_energia+
    $lines($searchall(@,$format(/dal \d\d\/\d\d\/\d\d\d\d al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese_abbr))) {
      %spesa_materia_energia+=$search(@,/(%N)$/)
    }
    $with($search(@,$format(/(In F1 dal \d\d\/\d\d\/\d\d\d\d al \d\d\/$0 [^]+)(dal|Totale|$)/,_mese_abbr))) {   
      $tokens($search(@,/F1.+\/kWh (%N kWh \d+)/)) {
        %prezzo_energia[0] $skip() %energia_attiva[0]
      }
      $tokens($search(@,/F2.+\/kWh (%N kWh \d+)/)) {
        %prezzo_energia[1] $skip() %energia_attiva[1]
      }
      $tokens($search(@,/F3.+\/kWh (%N kWh \d+)/)) {
        %prezzo_energia[2] $skip() %energia_attiva[2]
      }
    }
  }
  $between("SPESA PER IL TRASPORTO","TOTALE SPESA PER IL TRASPORTO") {
    $lines($searchall(@,$format(/$0[^]+?mesi %N (%N)/,_mese_full))) %trasporto_gestione+
    $lines($searchall(@,$format(/dal \d\d\/\d\d\/\d\d\d\d al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese_abbr))) {
      %trasporto_gestione+=$search(@,/(%N)$/)
    }
    %potenza[0:3]=$search(@,$format(/QUOTA POTENZA\n$0.+ kW (%N) mesi %N/,_mese_full))
  }
  $between("SPESA PER ONERI DI SISTEMA","TOTALE SPESA PER ONERI") {
    $lines($searchall(@,$format(/$0[^]+?mesi %N (%N)/,_mese_full))) %oneri+
    $lines($searchall(@,$format(/dal \d\d\/\d\d\/\d\d\d\d al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese_abbr))) {
      %oneri+=$search(@,/(%N)$/)
    }
  }
  $between("IMPOSTE ED IVA","TOTALE IMPOSTE ED IVA") {
    iva=$search(@,/IVA (\d+%)/)
    $with($search(@,/ACCISA SULL'ENERGIA ELETTRICA\n((dal.+\n)+)/)) {
      %accise=$search(@,$format(/al \d\d\/$0.+ (%N)/,_mese_abbr))
    }
  }
  %altre_partite=$search(@,/TOTALE ALTRE PARTITE SOGGETTE IVA (%N)/)
  imponibile=spesa_materia_energia + trasporto_gestione + oneri + altre_partite + accise
}
### End Script
### End Box

### Box
### Name Prossima pagina
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.15071335
### Y 0.00890943
### W 0.041901693
### H 0.028506272
### Script
$if ($isset(mese_fine)) {
  $if (mese_fattura != mese_fine) {
    $nexttable()
    $goto(nome_fornitore)
  }
}
### End Script
### End Box
