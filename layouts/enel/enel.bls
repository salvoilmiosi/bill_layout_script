### Bill Layout Script
### Language it_IT

### Box Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.03387097 0.096921325 0.34032258 0.044469774
### Script
$ifnot($contains(@,"Enel Energia - Mercato libero dell'energia")) {
  $error("Enel: Stringa di Convalida Errata")
}
### End Script
### End Box

### Box Calcolo Offset Dettaglio
### Type RECTANGLE
### Mode DEFAULT
### Page 3
### Rect 0.053306885 0.20998158 0.3093039 0.027190598
### Goto Label calcolo_offset
### Spacers
p+::offset_dettaglio
### End Spacers
### Script
$if($ate()) {
  $error("Enel: Dettaglio Mancante")
} $elif (@ != "DETTAGLIO IMPORTI IN BOLLETTA") {
  ::offset_dettaglio+=1
  $goto(calcolo_offset)
}
### End Script
### End Box

### Box Contenuto Dettaglio Costi
### Type RECTANGLE
### Mode RAW
### Flags NOTRIM
### Page 3
### Rect 0.048197635 0.21550742 0.71199036 0.76047003
### Goto Label dettaglio_costi
### Spacers
p+::offset_dettaglio
t-::spacer_dettaglio
### End Spacers
### Script
::_dettaglio+=@
### End Script
### End Box

### Box Spacer prima pagina dettaglio
### Type NOREAD
### Mode DEFAULT
### Page 3
### Rect 0.048348106 0.005701254 0.032232072 0.20980616
### Spacers
p+::offset_dettaglio
### End Spacers
### Script
::spacer_dettaglio=$box(height)
$ifnot($ate()) {
  ::offset_dettaglio+=1
  $goto(dettaglio_costi)
}
### End Script
### End Box

### Box Nome Fornitore
### Type NOREAD
### Mode DEFAULT
### Page 1
### Rect 0.038709678 0.030786773 0.22096774 0.061573543
### Goto Label nome_fornitore
### Script
fornitore="Enel Energia"
### End Script
### End Box

### Box Dati Fornitura
### Type RECTANGLE
### Mode RAW
### Page 1
### Rect 0.04623656 0.1717978 0.41827956 0.16039528
### Script
indirizzo_fornitura=$singleline($search(@,/Forniamo energia in ([^]+\d{5}.+\n)/))
%potenza[0:3]=$search(@,/(?:Potenza contrattualmente impegnata|Livello massimo di potenza) (\N)/)
### End Script
### End Box

### Box Ragione sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.5015466 0.18353352 0.39770216 0.031239748
### Script
ragione_sociale
### End Script
### End Box

### Box Numero cliente / POD
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.05251878 0.33476514 0.20437472 0.07914594
### Script
numero_cliente=$search(@,/N\u00b0 CLIENTE (.+)\n/)
codice_pod=$search(@,/CODICE POD (.+)$/)
### End Script
### End Box

### Box Numero / Data fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.27857563 0.38391757 0.21629238 0.030719101
### Script
numero_fattura=$search(@,/N\. Fattura (\N)/)
data_fattura=$date(@,"%d/%m/%Y",/Del (\D)/)
### End Script
### End Box

### Box Totale Fattura / Scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.51906633 0.36861986 0.21763146 0.046880633
### Script
%totale_fattura=$search(@,/(\N)/)
data_scadenza=$date(@,"%d/%m/%Y",/Entro il (\D)/)
### End Script
### End Box

### Box Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.27988616 0.4306124 0.22106262 0.020122074
### Script
mese_fattura=$month(@,"%b. %Y")
mese_fine=$month(@,"%b. %Y",/\D - (\D)/)
$if($isset(mese_fine)) {
  mese_fattura=$month_add(mese_fattura,::calc_mese)
  ::calc_mese+=1
}
_mese=$date_format(mese_fattura,"%m/%y")
### End Script
### End Box

### Box Dettaglio Fiscale
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### Rect 0.043979228 0.67284954 0.6890491 0.16947563
### Script
%cts=$search(@,/CORRISPETTIVO TARIFFARIO SPECIFICO.+ (\N)/)
iva=$search(@,/IVA.* (\N%)/)
### End Script
### End Box

### Box Consumi Rilevati
### Type PAGE
### Mode LAYOUT
### Page 2
### Rect 0.057569835 0.42134714 0.6868947 0.095169425
### Script
_prev_month=$month_add(mese_fattura,-1)
$if($contains(@,"Energia attiva")) {
  $step($captures(@,$format(/\d\d\/$0 (\N) (\N) (\N)/,$date_format(mese_fattura,"%m/%Y")))) {
    %energia_attiva_rilevata[0] %energia_attiva_rilevata[1] %energia_attiva_rilevata[2]
  }
  $step($captures(@,$format(/\d\d\/$0 (\N) (\N) (\N)/,$date_format(_prev_month,"%m/%Y")))) {
    %energia_attiva_rilevata[0]-=@ %energia_attiva_rilevata[1]-=@ %energia_attiva_rilevata[2]-=@
  }
}
$if($contains(@,"Energia reattiva")) {
  $step($captures(@,$format(/\d\d\/$0 \N \N \N (\N) (\N) (\N)/,$date_format(mese_fattura,"%m/%Y")))) {
    %energia_reattiva[0] %energia_reattiva[1] %energia_reattiva[2]
  }
  $step($captures(@,$format(/\d\d\/$0 \N \N \N (\N) (\N) (\N)/,$date_format(_prev_month,"%m/%Y")))) {
    %energia_reattiva[0]-=@ %energia_reattiva[1]-=@ %energia_reattiva[2]-=@
  }
}
### End Script
### End Box

### Box Parse Dettaglio Costi
### Type NOREAD
### Mode DEFAULT
### Page 3
### Rect 0.0903738 0.0063137496 0.053058922 0.03473528
### Script
$with(&::_dettaglio) {
  $step($captures(@,$format(/Energia (?:fascia F1|(?:ore di )?picco|mese) dal \d\d\/$0.+kWh (\N) (\N)/,_mese))) {
    %prezzo_energia[0] %energia_attiva[0]
  }
  $step($captures(@,$format(/Energia (?:fascia F2|(?:ore di )?fuori picco) dal \d\d\/$0.+kWh (\N) (\N)/,_mese))) {
    %prezzo_energia[1] %energia_attiva[1]
  }
  $step($captures(@,$format(/Energia fascia F3 dal \d\d\/$0.+kWh (\N) (\N)/,_mese))) {
    %prezzo_energia[2] %energia_attiva[2]
  }
  $with($search(@,$format(/Energia attiva dal \d\d\/$0.+\n((.*Energia Reattiva.*\n)+)/,_mese))) {
    %penale_reattiva_inf75=$search(@,/entro il 75%.+ (\N)\n/)
    %penale_reattiva_sup75=$search(@,/oltre il 75%.+ (\N)\n/)
  }
  %pcv=$search(@,$format(/Commercializzazione vendita dal \d\d\/$0.+mesi 1 (\N)/,_mese))
  %sbilanciamento=$search(@,$format(/Corrispettivo di Sbilanciamento dal \d\d\/$0.+kWh (\N)/,_mese))
  %accise=$search(@,$format(/Accisa sull'energia elettrica \(entro 200000\) dal \d\d\/$0.+kWh \N \N (\N)/,_mese))

  $between("SPESA PER L'ENERGIA", "Totale spesa per l'energia") {
    $with($matches(@,$format(/al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese))) {
      ^spesa_materia_energia=$matches(@,/(\N)(?:\n|$)/)
    }
  }
  $between("SPESA PER IL TRASPORTO", "Totale spesa per il trasporto") {
    $with($matches(@,$format(/al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese))) {
      ^trasporto_gestione+=$matches(@,/mesi.?1 (\N)/)
      ^trasporto_gestione+=$matches(@,/\/kWh \N \N (\N)/)
      ^trasporto_gestione+=$matches(@,/\/kVarh \N \N (\N)/)
      %potenza[0:3]=$search(@,/kW (\N)/)
    }
  }
  $between("SPESA ONERI DI SISTEMA", "Totale spesa oneri di sistema") {
    $foreach($matches(@,$format(/al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese))) {
      %oneri+=$search(@,/mesi.?1 (\N)/)
      %oneri+=$search(@,/\/kWh \N \N (\N)/)
    }
  }
	
  imponibile=$sum(spesa_materia_energia, trasporto_gestione, oneri, accise)
}
### End Script
### End Box

### Box Prossima pagina
### Type NOREAD
### Mode DEFAULT
### Page 3
### Rect 0.15071335 0.00890943 0.041901693 0.028506272
### Script
$if ($isset(mese_fine)) {
  $if (mese_fattura != mese_fine) {
    $nexttable()
    $goto(nome_fornitore)
  }
}
### End Script
### End Box
