### Bill Layout Script
### Language it

### Box
### Name Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.03387097
### Y 0.096921325
### W 0.34032258
### H 0.044469774
### Script
$ifnot($contains(@,"Enel Energia - Mercato libero dell'energia")) {
  $error("Enel: Stringa di Convalida Errata")
}
### End Script
### End Box

### Box
### Name Calcolo Offset Dettaglio
### Type RECTANGLE
### Mode DEFAULT
### Page 3
### X 0.053306885
### Y 0.20998158
### W 0.3093039
### H 0.027190598
### Goto Label calcolo_offset
### Spacers
p+::offset_dettaglio
### End Spacers
### Script
$if ($ate()) {
  $error("Enel: Dettaglio Mancante")
} $elif (@ != "DETTAGLIO IMPORTI IN BOLLETTA") {
  ::offset_dettaglio+=1
  $goto(calcolo_offset)
}
### End Script
### End Box

### Box
### Name Contenuto Dettaglio Costi
### Type RECTANGLE
### Mode RAW
### Page 3
### X 0.048197635
### Y 0.21550742
### W 0.71199036
### H 0.76047003
### Goto Label dettaglio_costi
### Spacers
p+::offset_dettaglio y-::spacer_dettaglio h+::spacer_dettaglio
### End Spacers
### Script
::_dettaglio+=@
::_dettaglio+="\n"
### End Script
### End Box

### Box
### Name Spacer prima pagina dettaglio
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.048348106
### Y 0.005701254
### W 0.032232072
### H 0.20980616
### Spacers
p+::offset_dettaglio
### End Spacers
### Script
::spacer_dettaglio=$boxheight()
$ifnot($ate()) {
  ::offset_dettaglio+=1
  $goto(dettaglio_costi)
}
### End Script
### End Box

### Box
### Name Nome Fornitore
### Type NOREAD
### Mode DEFAULT
### Page 1
### X 0.038709678
### Y 0.030786773
### W 0.22096774
### H 0.061573543
### Goto Label nome_fornitore
### Script
fornitore="Enel Energia"
### End Script
### End Box

### Box
### Name Dati Fornitura
### Type RECTANGLE
### Mode RAW
### Page 1
### X 0.04623656
### Y 0.1717978
### W 0.41827956
### H 0.16039528
### Script
indirizzo_fornitura=$search($singleline(@),/Forniamo energia in (.+) Denominazione/)
%potenza[0:3]=$coalesce(
  $search(@,/Potenza contrattualmente impegnata (%N)/),
  $search(@,/Livello massimo di potenza (%N)/))
### End Script
### End Box

### Box
### Name Dettaglio Fiscale
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.043979228
### Y 0.67284954
### W 0.6890491
### H 0.16947563
### Script
%cts=$search(@,/CORRISPETTIVO TARIFFARIO SPECIFICO.+ (%N)/)
iva=$search(@,/IVA.* (\d+%)/)
### End Script
### End Box

### Box
### Name Totale Fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.51906633
### Y 0.36861986
### W 0.21763146
### H 0.035925712
### Script
%totale_fattura
### End Script
### End Box

### Box
### Name Data fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.304905
### Y 0.3998688
### W 0.19443217
### H 0.015619874
### Script
data_fattura=$date(@,"%d/%m/%Y")
### End Script
### End Box

### Box
### Name Data scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.6628369
### Y 0.40455475
### W 0.074016795
### H 0.0117149055
### Script
data_scadenza=$date(@,"%d/%m/%Y")
### End Script
### End Box

### Box
### Name Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.27988616
### Y 0.4306124
### W 0.22106262
### H 0.020122074
### Script
mese_fattura=$month(@,"%b. %Y")
mese_fine=$month(@,"%b. %Y",/%D - (%D)/)
$if($isset(mese_fine)) {
  mese_fattura=$month_add(mese_fattura,::calc_mese)
  ::calc_mese+=1
}
_mese=$date_format(mese_fattura,"%m/%y")
### End Script
### End Box

### Box
### Name Numero cliente
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.054131683
### Y 0.35300916
### W 0.20437472
### H 0.02108683
### Script
numero_cliente
### End Script
### End Box

### Box
### Name Codice POD
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.05523641
### Y 0.39127785
### W 0.20768891
### H 0.017962856
### Script
codice_pod
### End Script
### End Box

### Box
### Name Numero fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.34577993
### Y 0.38737288
### W 0.15355723
### H 0.0124959
### Script
numero_fattura
### End Script
### End Box

### Box
### Name Ragione sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.5015466
### Y 0.18353352
### W 0.39770216
### H 0.031239748
### Script
ragione_sociale
### End Script
### End Box

### Box
### Name Consumi Rilevati
### Type PAGE
### Mode LAYOUT
### Page 2
### X 0.057569835
### Y 0.42134714
### W 0.6868947
### H 0.095169425
### Script
$if($contains(@,"Energia attiva")) {
  $tokens($search(@,$format(/\d\d\/$0 (%N %N %N)/,$date_format(mese_fattura,"%m/%Y")))) {
    %energia_attiva_rilevata[0] %energia_attiva_rilevata[1] %energia_attiva_rilevata[2]
  }
}
$if($contains(@,"Energia reattiva")) {
  $tokens($search(@,$format(/\d\d\/$0 %N %N %N (%N %N %N)/,$date_format(mese_fattura,"%m/%Y")))) {
    %energia_reattiva[0] %energia_reattiva[1] %energia_reattiva[2]
  }
}
### End Script
### End Box

### Box
### Name Parse Dettaglio Costi
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.0903738
### Y 0.0063137496
### W 0.053058922
### H 0.03473528
### Script
$with(::_dettaglio) {
  $tokens($search(@,$format(/Energia fascia F1 dal \d\d\/$0.+kWh (%N \d+)/,_mese))) {
    %prezzo_energia[0] %energia_attiva[0]
  }
  $tokens($search(@,$format(/Energia fascia F2 dal \d\d\/$0.+kWh (%N \d+)/,_mese))) {
    %prezzo_energia[1] %energia_attiva[1]
  }
  $tokens($search(@,$format(/Energia fascia F3 dal \d\d\/$0.+kWh (%N \d+)/,_mese))) {
    %prezzo_energia[2] %energia_attiva[2]
  }
  $tokens($search(@,$format(/Energia(?: ore di)? picco dal \d\d\/$0.+kWh (%N \d+)/,_mese))) {
    %prezzo_energia[0] %energia_attiva[0]
  }
  $tokens($search(@,$format(/Energia(?: ore di)? fuori picco dal \d\d\/$0.+kWh (%N \d+)/,_mese))) {
    %prezzo_energia[1] %energia_attiva[1]
  }
  $with($search(@,$format(/Energia attiva dal \d\d\/$0.+\n((.*Energia Reattiva.*\n)+)/,_mese))) {
    %penale_reattiva_inf75=$search(@,/entro il 75%.+ (%N)\n/)
    %penale_reattiva_sup75=$search(@,/oltre il 75%.+ (%N)\n/)
  }
  %pcv=$search(@,$format(/Commercializzazione vendita dal \d\d\/$0.+mesi 1 (%N)/,_mese))
  %sbilanciamento=$search(@,$format(/Corrispettivo di Sbilanciamento dal \d\d\/$0.+kWh (%N)/,_mese))
  %accise=$search(@,$format(/Accisa sull'energia elettrica \(entro 200000\) dal \d\d\/$0.+kWh %N \d+ (%N)/,_mese))

  $between("SPESA PER L'ENERGIA", "Totale spesa per l'energia") {
    $lines($searchall(@,$format(/al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese))) {
      %spesa_materia_energia+=$search(@,/(%N)$/)
    }
  }
  $between("SPESA PER IL TRASPORTO", "Totale spesa per il trasporto") {
    $with($searchall($singleline(@),$format(/al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese))) {
      $lines($searchall(@,/mesi.?1 (%N)/)) %trasporto_gestione+
      $lines($searchall(@,/\/kWh %N \d+ (%N)/)) %trasporto_gestione+
      $lines($searchall(@,/\/kVarh %N \d+ (%N)/)) %trasporto_gestione+
      %potenza[0:3]=$search(@,/kW (%N)/)
    }
  }
  $between("SPESA ONERI DI SISTEMA", "Totale spesa oneri di sistema") {
    $lines($searchall($singleline(@),$format(/al \d\d\/$0([^]+?)(dal|Totale|$)/,_mese))) {
      %oneri+=$search(@,/mesi.?1 (%N)/)
      %oneri+=$search(@,/\/kWh %N \d+ (%N)/)
    }
  }
	
  imponibile = spesa_materia_energia + trasporto_gestione + oneri + accise
}
### End Script
### End Box

### Box
### Name Prossima pagina
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.15071335
### Y 0.00890943
### W 0.041901693
### H 0.028506272
### Script
$if ($isset(mese_fine)) {
  $if (mese_fattura != mese_fine) {
    $nexttable()
    $goto(nome_fornitore)
  }
}
### End Script
### End Box
