### Bill Layout Script

### Box
### Name Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.0739910230
### Y 0.3029465377
### W 0.8291960359
### H 0.0248280428
### Goto Label convalida
### Spacers
p+::off_allegati
### End Spacers
### Script
$if($ate()) $error("Sorgenia (old): Stringa di Convalida Errata")
$if(@ != "BOLLETTA PER LA FORNITURA DI ENERGIA ELETTRICA NEL MERCATO LIBERO") {
  ::off_allegati+=1
  $goto(convalida)
}
### End Script
### End Box

### Box
### Name Lettura Dettaglio
### Type RECTANGLE
### Mode LAYOUT
### Page 3
### X 0.0402901024
### Y 0.0809578151
### W 0.9105559587
### H 0.8734164834
### Goto Label lettura_dati
### Spacers
p+::off_allegati p+::next_page
### End Spacers
### Script
$ifnot($ate() || $contains(@,"INFORMAZIONI PER I CLIENTI SORGENIA")) {
  ::_dettaglio+=@
  ::_dettaglio+="\n"
  ::next_page+=1
  $goto(lettura_dati)
}
### End Script
### End Box

### Box
### Name Nome Fornitore
### Type NOREAD
### Mode DEFAULT
### Page 1
### X 0.0612903237
### Y 0.0410490297
### W 0.3612903357
### H 0.0832383111
### Goto Label nome_fornitore
### Script
fornitore="Sorgenia"
### End Script
### End Box

### Box
### Name Numero cliente
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.4848197401
### Y 0.0104361940
### W 0.4648956358
### H 0.1047291458
### Spacers
p+::off_allegati
### End Spacers
### Script
numero_cliente=$search(@,/(\d+)/)
### End Script
### End Box

### Box
### Name Ragione sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.0766129047
### Y 0.1503705680
### W 0.3881048262
### H 0.0285062697
### Spacers
p+::off_allegati
### End Spacers
### Script
ragione_sociale
### End Script
### End Box

### Box
### Name Numero / Data fattura
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.0832053274
### Y 0.3307054937
### W 0.3437273502
### H 0.0385185778
### Spacers
p+::off_allegati
### End Spacers
### Script
numero_fattura=$search(@,/N. fattura: (.+)/)
data_fattura=$date(@,"%d/%m/%Y",/Emessa il (%D)/)
### End Script
### End Box

### Box
### Name POD / Indirizzo
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.4322580695
### Y 0.3285304308
### W 0.4813895822
### H 0.0624127090
### Spacers
p+::off_allegati
### End Spacers
### Script
codice_pod=$search(@,/per il punto di fornitura \(POD\) ([0-9A-Z]+)/)
indirizzo_fornitura=$singleline($search(@,/\ndi ([^]+\(..\))/))
### End Script
### End Box

### Box
### Name Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.0707351118
### Y 0.4228430092
### W 0.3672128916
### H 0.1100659072
### Spacers
p+::off_allegati
### End Spacers
### Script
mese_fattura=$month(@,"%B %Y")
mese_fine=$month(@,"%B %Y",/%D\/(%D)/)
mese_fattura=$month_add(mese_fattura,::calc_mese)
$ifnot($isset(mese_fattura)) {
  $error("Sorgenia (old): Mese Fattura Mancante")
}
### End Script
### End Box

### Box
### Name Data scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.6679323912
### Y 0.4228430092
### W 0.2348197252
### H 0.1100659072
### Spacers
p+::off_allegati
### End Spacers
### Script
data_scadenza=$date(@,"%d/%m/%Y",/DA SALDARE ENTRO (%D)/)
### End Script
### End Box

### Box
### Name Totale spesa
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.4387096763
### Y 0.4244267046
### W 0.2185639143
### H 0.1068985164
### Spacers
p+::off_allegati
### End Spacers
### Script
%totale_fattura=$search(@,/(%N) \u20ac/)
### End Script
### End Box

### Box
### Name Potenza Impegnata
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.6573299766
### Y 0.6641179323
### W 0.2675261796
### H 0.1249356866
### Spacers
p+::off_allegati
### End Spacers
### Script
%potenza[0:3]=$search(@,/Potenza impegnata: (%N)/)
### End Script
### End Box

### Box
### Name Energia Attiva
### Type RECTANGLE
### Mode LAYOUT
### Page 2
### X 0.0580177270
### Y 0.3854047954
### W 0.3553287387
### H 0.5330120325
### Spacers
p+::off_allegati
### End Spacers
### Script
### Il rettangolo e' alto perche' il layout shifta verticalmente

$with($search(@,/ENERGIA ATTIVA[^]+?F1 F2 F3\n([^]+)/)) {
  $tokens($search(@,$format(/$0 \w+ (%N %N %N)/,$date_format(mese_fattura,"%b - %y")))) {
    %energia_attiva_rilevata[0] %energia_attiva_rilevata[1] %energia_attiva_rilevata[2]
  }
}
### End Script
### End Box

### Box
### Name Energia Reattiva
### Type RECTANGLE
### Mode LAYOUT
### Page 2
### X 0.0580177270
### Y 0.3865450323
### W 0.2553085387
### H 0.5330120325
### Spacers
p+::off_allegati
### End Spacers
### Script
### Il rettangolo e' alto perche' il layout shifta verticalmente

$with($search(@,/ENERGIA REATTIVA[^]+?F1 F2 F3\n([^]+)/)) {
  $tokens($search(@,$format(/$0 (%N %N %N)/,$date_format(mese_fattura,"%b - %y")))) {
    %energia_reattiva[0] %energia_reattiva[1] %energia_reattiva[2]
  }
}
### End Script
### End Box

### Box
### Name Parsing Dettaglio
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.2668215632
### Y 0.0319813415
### W 0.0579877123
### H 0.0390942991
### Spacers
p+::off_allegati
### End Spacers
### Script
$with(::_dettaglio) {
  $lines($searchall(@,/Corrispettivo Tariffario Specifico dal \d+\/\d+\/\d+ al \d+\/\d+\/\d+ (%N)/)) %cts+
  iva=$search(@,/Iva (\d+%)/)

  $setbegin($indexof(@,$date_format(mese_fattura,"%B %Y")))
  $setend($indexof(@,"\n",$indexof(@,"FORNITURA DI")))

  %pcv=$search(@,/PCV euro\/cliente\/mese %N %N (%N)/)
  %spesa_materia_energia=$search(@,/DETTAGLIO DELLA SPESA PER LA MATERIA ENERGIA[^]+?(%N) DETTAGLIO/)
  %spesa_materia_energia=$search(@,/DETTAGLIO DELLA SPESA PER L\u2019ENERGIA ELETTRICA[^]+?(%N) DETTAGLIO/)
  %trasporto_gestione=$search(@,/DETTAGLIO DELLA SPESA PER TRASPORTO ENERGIA, GESTIONE CONTATORE E ONERI DI SISTEMA[^]+?(%N) IMPOSTE/)

  $tokens($search(@,/Prezzo F1 euro\/kWh (%N %N)/)) {
    %prezzo_energia[0] %energia_attiva[0]
  }
  $tokens($search(@,/Prezzo F2 euro\/kWh (%N %N)/)) {
    %prezzo_energia[1] %energia_attiva[1]
  }
  $tokens($search(@,/Prezzo F3 euro\/kWh (%N %N)/)) {
    %prezzo_energia[2] %energia_attiva[2]
  }
  $tokens($coalesce(
  $search(@,/Prezzo Picco euro\/kWh (%N %N)/),
  $search(@,/Prezzo Ore Giorno euro\/kWh (%N %N)/))) {
    %prezzo_energia[0] %energia_attiva[0]
  }
  $tokens($coalesce(
  $search(@,/Prezzo Fuori Picco euro\/kWh (%N %N)/),
  $search(@,/Prezzo Ore Notte euro\/kWh (%N %N)/))) {
    %prezzo_energia[1] %energia_attiva[1]
  }
  %prezzo_energia[0:3]=$search(@,/Prezzo energia euro\/kWh (%N)/)
  %prezzo_energia[0:3]=$search(@,/Prezzo energia monorario euro\/kWh (%N)/)

  %sbilanciamento=$search(@,/Incremento prezzo euro\/kWh (%N)/)

  %imponibile=$search(@,/(%N)$/)

  %penale_reattiva_inf75=$search(@,/Energia Reattiva tra il \d+% e 75% attiva euro\/kVARh %N %N (%N)/)
  %penale_reattiva_sup75=$search(@,/Energia Reattiva oltre il 75% attiva euro\/kVARh %N %N (%N)/)

  %potenza[0:3]=$search(@,/Quota Potenza euro\/kW\/mese %N (%N)/)
  %accise=$search(@,/Imposta erariale.+ (%N)\n/)
}
### End Script
### End Box

### Box
### Name Prossima pagina
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.3304147422
### Y 0.0285062715
### W 0.0576036870
### H 0.0447955690
### Spacers
p+::off_allegati
### End Spacers
### Script
$if($isset(mese_fine)) {
  $if(mese_fattura != mese_fine) {
    ::calc_mese+=1
    $nexttable()
    $goto(nome_fornitore)
  }
}
### End Script
### End Box
