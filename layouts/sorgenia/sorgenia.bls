### Bill Layout Script
### Language it

### Box
### Name Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.0753814578
### Y 0.3108103275
### W 0.8291960359
### H 0.0248280428
### Script
$if(@ != "BOLLETTA PER LA FORNITURA DI ENERGIA ELETTRICA NEL MERCATO LIBERO")
  $error("Sorgenia: Stringa di Convalida Errata")
### End Script
### End Box

### Box
### Name Lettura Dettaglio
### Type RECTANGLE
### Mode LAYOUT
### Page 3
### X 0.0402901024
### Y 0.0809578151
### W 0.9105559587
### H 0.8643101454
### Goto Label lettura_dati
### Spacers
p+::next_page
### End Spacers
### Script
$ifnot($ate() || $contains(@,"INFORMAZIONI PER I CLIENTI SORGENIA")) {
  ::_dettaglio+=@
  ::_dettaglio+="\n"
  ::next_page+=1
  $goto(lettura_dati)
}
### End Script
### End Box

### Box
### Name Nome Fornitore
### Type NOREAD
### Mode DEFAULT
### Page 1
### X 0.0612903237
### Y 0.0410490297
### W 0.3612903357
### H 0.0832383111
### Goto Label nome_fornitore
### Script
fornitore="Sorgenia"
### End Script
### End Box

### Box
### Name Numero cliente
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.4848197401
### Y 0.0104361940
### W 0.4648956358
### H 0.1047291458
### Script
numero_cliente=$search(@,/CODICE CLIENTE (.+)\n/)
### End Script
### End Box

### Box
### Name Ragione sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.0766129047
### Y 0.1503705680
### W 0.3881048262
### H 0.0285062697
### Script
ragione_sociale
### End Script
### End Box

### Box
### Name Numero / Data Fattura
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.0783666149
### Y 0.3364067376
### W 0.3861495256
### H 0.0398760438
### Script
numero_fattura=$search(@,/N\. fattura (.+)/)
data_fattura=$date(@,"%d/%m/%Y",/Emessa il (%D)/)
### End Script
### End Box

### Box
### Name POD / Indirizzo
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.4709677398
### Y 0.3353719413
### W 0.4604218304
### H 0.0511730909
### Script
codice_pod=$search(@,/per il punto di fornitura \(POD\) ([0-9A-Z]+)/)
indirizzo_fornitura=$singleline($search(@,/\ndi ([^]+\(..\))/))
### End Script
### End Box

### Box
### Name Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.0735887066
### Y 0.4846065938
### W 0.3114919364
### H 0.0235176720
### Script
mese_fattura=$month(@,"%B %Y")
mese_fine=$month(@,"%B %Y",/%D\/(%D)/)
mese_fattura=$month_add(mese_fattura,::calc_mese)
$ifnot($isset(mese_fattura)) {
  $error("Sorgenia: Mese Fattura Mancante")
}
### End Script
### End Box

### Box
### Name Data scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.7051532269
### Y 0.4358669817
### W 0.2348197252
### H 0.0494381189
### Script
data_scadenza=$date(@,"%d/%m/%Y",/DA SALDARE ENTRO (%D)/)
### End Script
### End Box

### Box
### Name Totale spesa
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.3145161271
### Y 0.4354332685
### W 0.3679435551
### H 0.0334948674
### Script
%totale_fattura
### End Script
### End Box

### Box
### Name Potenza Impegnata
### Type RECTANGLE
### Mode LAYOUT
### Page 2
### X 0.6946011186
### Y 0.0855188146
### W 0.2675261796
### H 0.0535917915
### Script
%potenza[0:3]=$search(@,/Potenza impegnata: (%N)/)
### End Script
### End Box

### Box
### Name Energia Attiva
### Type RECTANGLE
### Mode LAYOUT
### Page 2
### X 0.0580177270
### Y 0.3854047954
### W 0.3553287387
### H 0.5330120325
### Script
### Il rettangolo e' alto perche' il layout shifta verticalmente

$with($search(@,/ENERGIA ATTIVA[^]+?F1 F2 F3\n([^]+)/)) {
  $tokens($search(@,$format(/$0 \w+ (%N %N %N)/,$date_format(mese_fattura,"%b - %y")))) {
    %energia_attiva_rilevata[0] %energia_attiva_rilevata[1] %energia_attiva_rilevata[2]
  }
}
### End Script
### End Box

### Box
### Name Energia Reattiva
### Type RECTANGLE
### Mode LAYOUT
### Page 2
### X 0.0580177270
### Y 0.3865450323
### W 0.2352941036
### H 0.5330120325
### Script
### Il rettangolo e' alto perche' il layout shifta verticalmente

$with($search(@,/ENERGIA REATTIVA[^]+?F1 F2 F3\n([^]+)/)) {
  $tokens($search(@,$format(/$0 (%N %N %N)/,$date_format(mese_fattura,"%b - %y")))) {
    %energia_reattiva[0] %energia_reattiva[1] %energia_reattiva[2]
  }
}
### End Script
### End Box

### Box
### Name Parsing Dettaglio
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.2759512067
### Y 0.0330570489
### W 0.0579877123
### H 0.0390942991
### Script
$with(::_dettaglio) {
  $lines($searchall(@,/Corrispettivo Tariffario Specifico dal \d+\/\d+\/\d+ al \d+\/\d+\/\d+ (%N)/)) %cts+
  iva=$search(@,/Iva (\d+%)/)

  $setbegin($indexof(@,$date_format(mese_fattura,"%B %Y")))
  $setend($indexof(@,"\n",$indexof(@,"FORNITURA DI")))

  %pcv=$search(@,/PCV euro\/cliente\/mese %N %N (%N)/)
  %spesa_materia_energia=$search(@,/DETTAGLIO DELLA SPESA PER LA MATERIA ENERGIA[^]+?(%N) DETTAGLIO/)
  %spesa_materia_energia=$search(@,/DETTAGLIO DELLA SPESA PER L\u2019ENERGIA ELETTRICA[^]+?(%N) DETTAGLIO/)
  %trasporto_gestione=$search(@,/DETTAGLIO DELLA SPESA PER TRASPORTO ENERGIA, GESTIONE CONTATORE E ONERI DI SISTEMA[^]+?(%N) IMPOSTE/)

  $tokens($search(@,/Prezzo F1 euro\/kWh (%N %N)/)) {
    %prezzo_energia[0] %energia_attiva[0]
  }
  $tokens($search(@,/Prezzo F2 euro\/kWh (%N %N)/)) {
    %prezzo_energia[1] %energia_attiva[1]
  }
  $tokens($search(@,/Prezzo F3 euro\/kWh (%N %N)/)) {
    %prezzo_energia[2] %energia_attiva[2]
  }
  $tokens($coalesce(
  $search(@,/Prezzo Picco euro\/kWh (%N %N)/),
  $search(@,/Prezzo Ore Giorno euro\/kWh (%N %N)/))) {
    %prezzo_energia[0] %energia_attiva[0]
  }
  $tokens($coalesce(
  $search(@,/Prezzo Fuori Picco euro\/kWh (%N %N)/),
  $search(@,/Prezzo Ore Notte euro\/kWh (%N %N)/))) {
    %prezzo_energia[1] %energia_attiva[1]
  }
  %prezzo_energia[0:3]=$search(@,/Prezzo energia euro\/kWh (%N)/)
  %prezzo_energia[0:3]=$search(@,/Prezzo energia monorario euro\/kWh (%N)/)

  %sbilanciamento=$search(@,/Incremento prezzo euro\/kWh (%N)/)

  %imponibile=$search(@,/(%N)$/)

  %penale_reattiva_inf75=$search(@,/Energia Reattiva tra il \d+% e 75% attiva euro\/kVARh %N %N (%N)/)
  %penale_reattiva_sup75=$search(@,/Energia Reattiva oltre il 75% attiva euro\/kVARh %N %N (%N)/)

  %potenza[0:3]=$search(@,/Quota Potenza euro\/kW\/mese %N (%N)/)
  %accise=$search(@,/Imposta erariale.+ (%N)\n/)
}
### End Script
### End Box

### Box
### Name Prossima pagina
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.3425875902
### Y 0.0285062715
### W 0.0576036870
### H 0.0447955690
### Script
$if($isset(mese_fine)) {
  $if(mese_fattura != mese_fine) {
    ::calc_mese+=1
    $nexttable()
    $goto(nome_fornitore)
  }
}
### End Script
### End Box
