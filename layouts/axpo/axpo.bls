### Bill Layout Script
### Language it_IT

### Box Convalida Layout
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 1 0.78485096 0.023945268 0.20306204 0.07639681
### Script
$if(@ != "BOLLETTA\nENERGIA\nELETTRICA\nFornitura del Mercato Libero")
  $error("Axpo: Stringa di Convalida Errata")
### End Script
### End Box

### Box Nome Fornitore
### Type BOX_NOREAD
### Mode MODE_DEFAULT
### Rect 1 0.020967742 0.021664767 0.23548387 0.069555305
### Goto Label nome_fornitore
### Script
fornitore="Axpo"
### End Script
### End Box

### Box Ragione sociale
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 1 0.51044726 0.16909401 0.4719575 0.050533842
### Script
ragione_sociale
### End Script
### End Box

### Box Totale da pagare / scadenza
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 1 0.6150193 0.3080678 0.35555232 0.10002199
### Script
%totale_fattura=$search(@,/TOTALE DA PAGARE (%N)/)
data_scadenza=$date(@,"%d.%m.%Y",/entro il (%D)/)
### End Script
### End Box

### Box Dati bolletta
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 1 0.0315568 0.09791284 0.22615707 0.31941897
### Script
numero_fattura=$search(@,/N\. fattura (\d+)/)
data_fattura=$date(@,"%d.%m.%Y",/N\. fattura \d+ del (%D)/)
mese_fattura=$month(@,"%d.%m.%Y",/PERIODO %D - (%D)/)
numero_cliente=$search(@,/CODICE CLIENTE (.+)\n/)
codice_pod=$search(@,/CODICE POD ([0-9A-Z]+)/)
indirizzo_fornitura=$singleline($search(@,/FORNIAMO ENERGIA IN\n([^]+?\d{5}.+)\n/))
::multipod=$isempty(codice_pod)
_mese=$date_format(mese_fattura,"%m.%Y")
### End Script
### End Box

### Box Dettaglio Costi
### Type BOX_RECTANGLE
### Mode MODE_RAW
### Rect 1 0.047653962 0.65514416 0.4389663 0.23846543
### Script
iva=$search(@,/Importo IVA (\d+%)/)
$ifnot(::multipod) {
  %spesa_materia_energia=$search(@,/SPESA PER LA MATERIA ENERGIA (%N) \u20ac/)
  %trasporto_gestione=$search(@,/SPESA PER IL TRASPORTO E LA GESTIONE DEL CONTATORE (%N) \u20ac/)
  %oneri=$search(@,/SPESA PER ONERI DI SISTEMA (%N) \u20ac/)
  %accise=$search(@,/IMPOSTE (%N) \u20ac/)
  %imponibile=$search(@,/imponibile di (%N)/)
}
### End Script
### End Box

### Box Dati fornitura (multipod)
### Type BOX_RECTANGLE
### Mode MODE_RAW
### Rect 2 0.04193548 0.13418806 0.22741936 0.11324638
### Spacers
p+::next_page
### End Spacers
### Script
$if(::multipod) {
  codice_pod=$search(@,/CODICE POD ([0-9A-Z]+)/)
  indirizzo_fornitura=$singleline($search(@,/FORNIAMO ENERGIA IN\n([^]+)/))
}
### End Script
### End Box

### Box Energia Reattiva
### Type BOX_PAGE
### Mode MODE_RAW
### Rect 2 0.03181463 0.2576967 0.94399184 0.25427598
### Spacers
p+::next_page
### End Spacers
### Script
%imponibile=$search(@,/TOTALE IMPONIBILE (%N)/)
$if($contains(@,"Energia Reattiva")) {
  %energia_reattiva=$search($search(@,/CONSUMI E LETTURE([^]+)Consumi fatturati/),
    $format(/\d\d\.$0.+ (%N)\n/,_mese))
}
### End Script
### End Box

### Box Lettura Quadro Dettaglio
### Type BOX_RECTANGLE
### Mode MODE_RAW
### Rect 3 0.032258064 0.090079814 0.95483875 0.8289623
### Goto Label quadro_dettaglio
### Spacers
p+::next_page
### End Spacers
### Script
$ifnot($ate() || $contains(@,"DATI FORNITURA") || $contains(@,"COMUNICAZIONI")) {
  _dettaglio+=@
  _dettaglio+="\n"
  ::next_page+=1
  $goto(quadro_dettaglio)
}
### End Script
### End Box

### Box Parsing Quadro Dettaglio
### Type BOX_NOREAD
### Mode MODE_RAW
### Rect 3 0.21504012 0.053452756 0.04193318 0.029393759
### Script
$with(_dettaglio) {
  %sbilanciamento=$search($search(@,/Sbilanciamento\n((dal.+\n)+)/),
    $format(/al \d\d\.$0 Euro\/kWh (%N)/,_mese))
  %pcv=$search($search(@,/Prezzo Commercializzazione Vendita\n((dal.+\n)+)/),
    $format(/al \d\d\.$0 euro\/\w+ %N %N (%N)/,_mese))
  $with($coalesce(
    $search(@,/Prezzo Energia\n((dal.+\n)+)/),
    $search(@,/Prezzo Indicizzato\n((dal.+\n)+)/)))
  {
    $tokens($search(@,$format(/al \d\d\.$0 F1 Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[0] %energia_attiva[0]
    }
    $tokens($search(@,$format(/al \d\d\.$0 F2 Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[1] %energia_attiva[1]
    }
    $tokens($search(@,$format(/al \d\d\.$0 F3 Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[2] %energia_attiva[2]
    }

    $tokens($search(@,$format(/al \d\d\.$0 PEAK Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[0] %energia_attiva[0]
    }
    $tokens($search(@,$format(/al \d\d\.$0 OFFPEAK Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[1] %energia_attiva[1]
    }

    $tokens($search(@,$format(/al \d\d\.$0 Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia %energia_attiva
    }
  }

  $lines($searchall($search(@,/Energia Reattiva entro 75%\n((dal.+\n)+)/),
    $format(/al \d\d\.$0 F[1-3] Euro\/kVarh %N %N (%N)/))) %penale_reattiva_inf75+
  $lines($searchall($search(@,/Energia Reattiva oltre 75%\n((dal.+\n)+)/),
    $format(/al \d\d\.$0 F[1-3] Euro\/kVarh %N %N (%N)/))) %penale_reattiva_sup75+
  
  %spesa_materia_energia=$search(@,/SPESA PER LA MATERIA ENERGIA IMPORTO \u20ac (%N)/)
  %trasporto_gestione=$search(@,/SPESA PER IL TRASPORTO IMPORTO \u20ac (%N)/)
  %oneri=$search(@,/ONERI DI SISTEMA IMPORTO \u20ac (%N)/)

  %potenza[0:3]=$search(@,$format(/al \d\d\.$0 Euro\/kW %N (%N)/,_mese))
  %accise=$search($search(@,/Imposta erariale di consumo \(accisa\)\n[^]*?- Imposta erariale\n((dal.+\n)+)/),
    $format(/al \d\d\.$0 Euro\/kWh %N %N (%N)/,_mese))
}
### End Script
### End Box

### Box Prossima Pagina
### Type BOX_RECTANGLE
### Mode MODE_DEFAULT
### Rect 3 0.03840065 0.0989254 0.22611547 0.032341655
### Spacers
p+::next_page
### End Spacers
### Script
$if(@ == "DATI FORNITURA") {
  ::next_page+=1
  $nexttable()
  $goto(nome_fornitore)
}
### End Script
### End Box
