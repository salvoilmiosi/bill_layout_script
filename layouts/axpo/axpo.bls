### Bill Layout Script

### Box
### Name Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.7848509550
### Y 0.0239452682
### W 0.2030620426
### H 0.0763968080
### Script
$if(@ != "BOLLETTA\nENERGIA\nELETTRICA\nFornitura del Mercato Libero")
  $error("Layout Invalido")
### End Script
### End Box

### Box
### Name Nome Fornitore
### Type NOREAD
### Mode DEFAULT
### Page 1
### X 0.0209677424
### Y 0.0216647666
### W 0.2354838699
### H 0.0695553049
### Goto Label nome_fornitore
### Script
fornitore="Axpo"
### End Script
### End Box

### Box
### Name Ragione sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.5104472637
### Y 0.1690940112
### W 0.4719575047
### H 0.0505338423
### Script
ragione_sociale
### End Script
### End Box

### Box
### Name Totale da pagare / scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.6150193214
### Y 0.3080677986
### W 0.3555523157
### H 0.1000219882
### Script
%totale_fattura=$search(@,/TOTALE DA PAGARE (%N)/)
data_scadenza=$date(@,"%d.%m.%Y",/entro il (%D)/)
### End Script
### End Box

### Box
### Name Dati bolletta
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.0315568000
### Y 0.0979128405
### W 0.2261570692
### H 0.3194189668
### Script
numero_fattura=$search(@,/N\. fattura (\d+)/)
data_fattura=$date(@,"%d.%m.%Y",/N\. fattura \d+ del (%D)/)
mese_fattura=$month(@,"%d.%m.%Y",/PERIODO %D - (%D)/)
numero_cliente=$search(@,/CODICE CLIENTE (.+)\n/)
codice_pod=$search(@,/CODICE POD ([0-9A-Z]+)/)
indirizzo_fornitura=$search($singleline(@),/FORNIAMO ENERGIA IN (.+) CODICE POD/)
::multipod=$isempty(codice_pod)
_mese=$date_format(mese_fattura,"%m.%Y")
### End Script
### End Box

### Box
### Name Dettaglio Costi
### Type RECTANGLE
### Mode RAW
### Page 1
### X 0.0476539619
### Y 0.6551441550
### W 0.4389663041
### H 0.2384654284
### Script
iva=$search(@,/Importo IVA (\d+%)/)
$ifnot(::multipod) {
  %spesa_materia_energia=$search(@,/SPESA PER LA MATERIA ENERGIA (%N) \u20ac/)
  %trasporto_gestione=$search(@,/SPESA PER IL TRASPORTO E LA GESTIONE DEL CONTATORE (%N) \u20ac/)
  %oneri=$search(@,/SPESA PER ONERI DI SISTEMA (%N) \u20ac/)
  %accise=$search(@,/IMPOSTE (%N) \u20ac/)
  %imponibile=$search(@,/imponibile di (%N)/)
}
### End Script
### End Box

### Box
### Name Dati fornitura (multipod)
### Type RECTANGLE
### Mode RAW
### Page 2
### X 0.0419354811
### Y 0.1341880560
### W 0.2274193615
### H 0.1132463813
### Spacers
p+::next_page
### End Spacers
### Script
$if(::multipod) {
  codice_pod=$search(@,/CODICE POD ([0-9A-Z]+)/)
  indirizzo_fornitura=$search($singleline(@),/FORNIAMO ENERGIA IN (.+)/)
}
### End Script
### End Box

### Box
### Name Energia Reattiva
### Type PAGE
### Mode RAW
### Page 2
### X 0.0318146311
### Y 0.2576966882
### W 0.9439918399
### H 0.2542759776
### Spacers
p+::next_page
### End Spacers
### Script
%imponibile=$search(@,/TOTALE IMPONIBILE (%N)/)
$if($contains(@,"Energia Reattiva")) {
  $with($search(@,/CONSUMI E LETTURE([^]+)Consumi fatturati/)) {
    %energia_reattiva=$search(@,$format(/\d\d\.$0.+ (%N)\n/,_mese))
  }
}
### End Script
### End Box

### Box
### Name Lettura Quadro Dettaglio
### Type RECTANGLE
### Mode RAW
### Page 3
### X 0.0322580636
### Y 0.0900798142
### W 0.9548387527
### H 0.8289623260
### Goto Label quadro_dettaglio
### Spacers
p+::next_page
### End Spacers
### Script
$ifnot($ate() || $contains(@,"DATI FORNITURA") || $contains(@,"COMUNICAZIONI")) {
  _dettaglio+=@
  _dettaglio+="\n"
  ::next_page+=1
  $goto(quadro_dettaglio)
}
### End Script
### End Box

### Box
### Name Parsing Quadro Dettaglio
### Type NOREAD
### Mode RAW
### Page 3
### X 0.2150401175
### Y 0.0534527563
### W 0.0419331789
### H 0.0293937586
### Script
$with(_dettaglio) {
  $with($search(@,/Sbilanciamento\n((dal.+\n)+)/)) {
    %sbilanciamento=$search(@,$format(/al \d\d\.$0 Euro\/kWh (%N)/,_mese))
  }
  $with($search(@,/Prezzo Commercializzazione Vendita\n((dal.+\n)+)/)) {
    %pcv=$search(@,$format(/al \d\d\.$0 euro\/\w+ %N %N (%N)/,_mese))
  }
  $with($coalesce(
    $search(@,/Prezzo Energia\n((dal.+\n)+)/),
    $search(@,/Prezzo Indicizzato\n((dal.+\n)+)/))
  ){
    $tokens($search(@,$format(/al \d\d\.$0 F1 Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[0] %energia_attiva[0]
    }
    $tokens($search(@,$format(/al \d\d\.$0 F2 Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[1] %energia_attiva[1]
    }
    $tokens($search(@,$format(/al \d\d\.$0 F3 Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[2] %energia_attiva[2]
    }

    $tokens($search(@,$format(/al \d\d\.$0 PEAK Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[0] %energia_attiva[0]
    }
    $tokens($search(@,$format(/al \d\d\.$0 OFFPEAK Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia[1] %energia_attiva[1]
    }

    $tokens($search(@,$format(/al \d\d\.$0 Euro\/kWh (%N %N)/,_mese))) {
      %prezzo_energia %energia_attiva
    }
  }

  $with($search(@,/Energia Reattiva entro 75%\n((dal.+\n)+)/)) {
    %penale_reattiva_inf75+=$search(@,$format(/al \d\d\.$0 F1 Euro\/kVarh %N %N (%N)/,_mese))
    %penale_reattiva_inf75+=$search(@,$format(/al \d\d\.$0 F2 Euro\/kVarh %N %N (%N)/,_mese))
    %penale_reattiva_inf75+=$search(@,$format(/al \d\d\.$0 F3 Euro\/kVarh %N %N (%N)/,_mese))
  }
  $with($search(@,/Energia Reattiva oltre 75%\n((dal.+\n)+)/)) {
    %penale_reattiva_sup75+=$search(@,$format(/al \d\d\.$0 F1 Euro\/kVarh %N %N (%N)/,_mese))
    %penale_reattiva_sup75+=$search(@,$format(/al \d\d\.$0 F2 Euro\/kVarh %N %N (%N)/,_mese))
    %penale_reattiva_sup75+=$search(@,$format(/al \d\d\.$0 F3 Euro\/kVarh %N %N (%N)/,_mese))
  }
  %spesa_materia_energia=$search(@,/SPESA PER LA MATERIA ENERGIA IMPORTO \u20ac (%N)/)
  %trasporto_gestione=$search(@,/SPESA PER IL TRASPORTO IMPORTO \u20ac (%N)/)
  %oneri=$search(@,/ONERI DI SISTEMA IMPORTO \u20ac (%N)/)

  %potenza[0:3]=$search(@,$format(/al \d\d\.$0 Euro\/kW %N (%N)/,_mese))
  $with($search(@,/Imposta erariale di consumo \(accisa\)\n[^]*?- Imposta erariale\n((dal.+\n)+)/)) {
    %accise=$search(@,$format(/al \d\d\.$0 Euro\/kWh %N %N (%N)/,_mese))
  }
}
### End Script
### End Box

### Box
### Name Prossima Pagina
### Type RECTANGLE
### Mode DEFAULT
### Page 3
### X 0.0384006500
### Y 0.0989253968
### W 0.2261154652
### H 0.0323416553
### Spacers
p+::next_page
### End Spacers
### Script
$if(@ == "DATI FORNITURA") {
  ::next_page+=1
  $nexttable()
  $goto(nome_fornitore)
}
### End Script
### End Box
