### Bill Layout Script
### Language it_IT

### Box
### Name Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.5398872
### Y 0.030786771
### W 0.3545528
### H 0.05587229
### Script
$if(@ != "Bolletta Energia Elettrica\nFornitura del Mercato Libero")
  $error("Axpo (old): Stringa di Convalida Errata")
### End Script
### End Box

### Box
### Name Nome Fornitore
### Type NOREAD
### Mode DEFAULT
### Page 1
### X 0.024193548
### Y 0.014823261
### W 0.19271415
### H 0.061848786
### Goto Label nome_fornitore
### Script
fornitore="Axpo"
### End Script
### End Box

### Box
### Name Numero fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.03548387
### Y 0.18015963
### W 0.47258064
### H 0.033067275
### Script
numero_fattura=$search(@,/Fattura N\u00b0 (\d+)/)
data_fattura=$date(@,"%d.%m.%Y",/del (%D)/)
### End Script
### End Box

### Box
### Name Scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.54147464
### Y 0.095020905
### W 0.43266767
### H 0.04615301
### Script
data_scadenza=$date(@,"%d.%m.%Y")
### End Script
### End Box

### Box
### Name Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.03548387
### Y 0.23147093
### W 0.20483871
### H 0.031927023
### Script
mese_fattura=$month(@,"%B %Y")
### End Script
### End Box

### Box
### Name Totale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### X 0.2435484
### Y 0.2599772
### W 0.2564516
### H 0.03990878
### Script
%totale_fattura
### End Script
### End Box

### Box
### Name Nome Cliente
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### X 0.021505376
### Y 0.10642341
### W 0.49462366
### H 0.06176359
### Script
numero_cliente=$search(@,/Codice Cliente: (\d+)/)
ragione_sociale=$search(@,/.+\n(.+)\n/)
partita_iva=$search(@,/Part\.IVA (.+)\n/)
### End Script
### End Box

### Box
### Name Dati fornitura singolo pod
### Type RECTANGLE
### Mode RAW
### Page 1
### X 0.067204304
### Y 0.61503536
### W 0.42183626
### H 0.30491322
### Script
$if($contains(@,"Dati di fornitura")) {
  indirizzo_fornitura=$search($singleline(@),/Indirizzo (.+\d{5}.+\(.{2}\))/)
  codice_pod=$search(@,/POD (.+)\n/)
  %potenza[0:3]=$search(@,/Potenza Impegnata (%N)/)
} $else {
  ::spacer_multipod=1
}
### End Script
### End Box

### Box
### Name Sintesi dati singolo pod
### Type RECTANGLE
### Mode RAW
### Page 1
### X 0.066170394
### Y 0.32745665
### W 0.42907363
### H 0.2335468
### Script
iva=$search(@,/IVA (\d%)/)
$ifnot(::spacer_multipod) {
  %spesa_materia_energia=$search(@,/Corrispettivi energia (%N)/)
  %spesa_materia_energia=$search(@,/Spese per l'energia (%N)/)
  %trasporto_gestione=$search(@,/Trasporto (%N)/)
  %trasporto_gestione=$search(@,/Spesa per trasporto, gestione contatore e oneri di sistema (%N)/)
}
### End Script
### End Box

### Box
### Name Dati fornitura multipod
### Type RECTANGLE
### Mode RAW
### Page 3
### X 0.03205999
### Y 0.09432077
### W 0.45019805
### H 0.24091297
### Goto Label dati_fattura_multipod
### Spacers
p+::next_page
### End Spacers
### Script
$if(::spacer_multipod) {
  indirizzo_fornitura=$search($singleline(@),/Indirizzo (.+\d{5}.+\(.{2}\))/)
  codice_pod=$search(@,/POD (.+)\n/)
  %potenza[0:3]=$search(@,/Potenza Impegnata (%N)/)
}
### End Script
### End Box

### Box
### Name Sintesi dati multipod
### Type RECTANGLE
### Mode RAW
### Page 3
### X 0.4935484
### Y 0.09236032
### W 0.45645162
### H 0.16419612
### Spacers
p+::next_page
### End Spacers
### Script
$if(::spacer_multipod) {
  %spesa_materia_energia=$search(@,/Corrispettivi energia \u20ac (%N)/)
  %spesa_materia_energia=$search(@,/Spese per l'energia \u20ac (%N)/)
  %trasporto_gestione=$search(@,/Trasporto \u20ac (%N)/)
  %trasporto_gestione=$search(@,/Spesa per trasporto, gestione contatore e oneri di sistema \u20ac (%N)/)
}
### End Script
### End Box

### Box
### Name Lettura Spese Energia
### Type RECTANGLE
### Mode LAYOUT
### Page 3
### X 0.033843674
### Y 0.09464082
### W 0.9073328
### H 0.87001145
### Goto Label lettura_spese
### Spacers
p+ ::spacer_multipod + ::next_page
### End Spacers
### Script
$if($ate() || $contains(@,"Comunicazioni") || $contains(@,"Dati di fornitura")) {
  $goto(parsing_spese)
}
_dettaglio+=@
_dettaglio+="\n"
::next_page+=1
$goto(lettura_spese)
### End Script
### End Box

### Box
### Name Parsing Spese Energia
### Type NOREAD
### Mode DEFAULT
### Page 3
### X 0.19357695
### Y 0.047890533
### W 0.046551988
### H 0.030786768
### Goto Label parsing_spese
### Script
$with(&_dettaglio) {
  $setbegin($indexof(@,$date_format(mese_fattura,"%B %Y")))
  $setend($indexof(@,"TOT. \u20ac", $indexof(@,"\n")))

  %imponibile=$search(@,/TOT\. \u20ac (%N)/)

  $tokens($search(@,/Prezzo Energia PEAK (%N kWh %N)/)) {
    %energia_attiva[0] $skip() %prezzo_energia[0]
  }
  $tokens($search(@,/Prezzo Energia OFFPEAK (%N kWh %N)/)) {
    %energia_attiva[1] $skip() %prezzo_energia[1]
  }

  $tokens($search(@,/Prezzo Energia F1 (%N kWh %N)/)) {
    %energia_attiva[0] $skip() %prezzo_energia[0]
  }
  $tokens($search(@,/Prezzo Energia F2 (%N kWh %N)/)) {
    %energia_attiva[1] $skip() %prezzo_energia[1]
  }
  $tokens($search(@,/Prezzo Energia F3 (%N kWh %N)/)) {
    %energia_attiva[2] $skip() %prezzo_energia[2]
  }

  %pcv=$search(@,/Prezzo Commercializzazione e Vendita .+\u20ac (%N)/)
  %sbilanciamento=$search(@,/Gestione sbilanciamento %N kWh (%N)/)
  %potenza[0:3]=$search(@,/Quota Potenza (%N) kW/)

  %penale_reattiva_inf75 =$search(@,/Energia Reattiva tra il 33 e il 75%.+ (%N)\n/)
  %penale_reattiva_inf75 =$search(@,/Energia Reattiva tra il 33 e il 75% F1.+ (%N)\n/)
  %penale_reattiva_inf75+=$search(@,/Energia Reattiva tra il 33 e il 75% F2.+ (%N)\n/)
  %penale_reattiva_inf75+=$search(@,/Energia Reattiva tra il 33 e il 75% F3.+ (%N)\n/)
  %penale_reattiva_sup75 =$search(@,/Energia Reattiva oltre il 75%.+ (%N)\n/)
  %penale_reattiva_sup75 =$search(@,/Energia Reattiva oltre il 75% F1.+ (%N)\n/)
  %penale_reattiva_sup75+=$search(@,/Energia Reattiva oltre il 75% F2.+ (%N)\n/)
  %penale_reattiva_sup75+=$search(@,/Energia Reattiva oltre il 75% F3.+ (%N)\n/)

  %accise=$search(@,/Imposta Erariale .+ \u20ac\/kWh.+ (%N)\n/)
}
### End Script
### End Box

### Box
### Name Controllo Prossima pagina
### Type RECTANGLE
### Mode DEFAULT
### Page 3
### X 0.040290087
### Y 0.09806158
### W 0.20306204
### H 0.029646521
### Spacers
p+ ::spacer_multipod + ::next_page
### End Spacers
### Script
$if(::spacer_multipod) {
  $if(@ == "Dati di fornitura") {
    ::next_page+=1
    $nexttable()
    $goto(nome_fornitore)
  }
}
### End Script
### End Box
